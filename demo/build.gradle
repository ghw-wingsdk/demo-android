apply plugin: 'com.android.application'

android {
    namespace "com.wa.sdk.demo"

    signingConfigs {
        config {
            keyAlias 'pcl-sdk-cert-dev'
            keyPassword '123456'
            storeFile file('../keystores/android-cert-dev.jks')
            storePassword '123456'
        }
    }

    defaultConfig {
        applicationId "com.ghw.sdk2"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 76
        versionName "4.4.1"
        signingConfig signingConfigs.config

        compileSdk 34
        //华为多语种设置
        resConfigs "en", "zh-rCN", "pt-rBR", "es-rES", "in-rID", "ru", "th-rTH", "zh-rHK", "zh-rTW"
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            debuggable false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def build_time = getDate()
            buildConfigField 'String', 'DEMO_BUILD_TIME',"\"${build_time}\""
            buildConfigField 'boolean', 'IS_TEST_REPOSITORY',IS_TEST_REPOSITORY
        }

        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.config

            def build_time = getDate()
            buildConfigField 'String', 'DEMO_BUILD_TIME',"\"${build_time}\""
            buildConfigField 'boolean', 'IS_TEST_REPOSITORY',IS_TEST_REPOSITORY
        }
    }

    flavorDimensions "server"
    productFlavors {

        qa{
            /**qa环境**/
            manifestPlaceholders.putAll([SDK_HTTP_BASE_URL         : 'http://172.16.100.53/sdk_bkd_qa/sdkapi/',
                                         SDK_TRACKING_HTTP_BASE_URL: 'http://172.16.100.58/data_collection_qa/data'])
        }

        pre {
            /**pre环境**/
            manifestPlaceholders.putAll([SDK_HTTP_BASE_URL         : 'http://sdk-test1.gamehollywood.com/sdk_bkd_qa/sdkapi',
                                         SDK_TRACKING_HTTP_BASE_URL: 'http://sdk-test2.gamehollywood.com/data_collection_qa/data/forward'])
        }

        pro{
            isDefault = true
            manifestPlaceholders.putAll([SDK_HTTP_BASE_URL         : 'https://api.wingsdk.com/sdkapi',
                                         SDK_TRACKING_HTTP_BASE_URL: 'https://api.wingsdk.com/data'])
        }
    }

    // 打包自动生成名字
    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            output.outputFileName = 'DemoSdk_imp_' + variant.productFlavors[0].name + "_" + ('true' == IS_TEST_REPOSITORY ?'test':'live') + '_build' + getDate() + '.apk'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

//获取时间戳
static def getDate() {
    def date = new Date()
    def formattedDate = date.format('HHmm')
    return formattedDate
}

dependencies {

    implementation 'com.google.android.material:material:1.4.0'

    // 基础模块（必选）
    implementation 'com.wa.sdk:wa-sdk-base:4.4.1'
    // 核心模块（必选）
    implementation 'com.wa.sdk:wa-sdk-core:4.4.1'
    // wa 客服系统（一般必接）
    implementation 'com.wa.sdk:wa-sdk-aihelp:4.2.0'
    // AppsFlyer模块（一般必接）
    implementation 'com.wa.sdk:wa-sdk-appsflyer:4.3.0'
    // 消息推送模块（可选）
    implementation 'com.wa.sdk:wa-sdk-firebase:4.2.0'
    // Facebook模块（可选）
    implementation 'com.wa.sdk:wa-sdk-facebook:4.3.0'
    // Google模块（可选）
    implementation 'com.wa.sdk:wa-sdk-google:4.3.0'
    // AdMob模块（可选）
    implementation 'com.wa.sdk:wa-sdk-admob:4.2.0'
    // 网页集成登录，GHG和R2 （可选）
    implementation 'com.wa.sdk:wa-sdk-weblogin:4.3.0'
    // 网页支付模块（可选）
    implementation 'com.wa.sdk:wa-sdk-webpay:4.2.0'
    // nowgg 模块（可选）
    implementation 'com.wa.sdk:wa-sdk-nowgg:4.4.1'


//    华为 HMS
//    implementation 'com.wa.sdk:wa-sdk-huaweihms:3.18.0'
    // Aptoide模块（可选）
    implementation 'com.wa.sdk:wa-sdk-aptoide:3.18.0'
//    wa hup模块
//    implementation 'com.wa.sdk:wa-sdk-hup:3.8.0'
    // 应用墙模块（可选）
//    implementation 'com.wa.sdk:wa-sdk-apw:3.8.3'
    // WA广告模块（可选）
//    implementation 'com.wa.sdk:wa-sdk-ad:3.8.3'
    // Chartboost模块（可选）
    implementation 'com.wa.sdk:wa-sdk-chartboost:3.9.7'
    // Twitter推送模块（可选）
    implementation 'com.wa.sdk:wa-sdk-twitter:3.9.0'
    // Instagram推送模块（可选）
    implementation 'com.wa.sdk:wa-sdk-instagram:3.8.4'
    // VK模块（可选）
    implementation 'com.wa.sdk:wa-sdk-vk:3.9.0'
}

//谷歌推送
apply plugin: 'com.google.gms.google-services'

//华为
//apply plugin: 'com.huawei.agconnect'
//agcp{
//    enableAPMS true
//}
