apply plugin: 'com.android.application'

android {
    namespace "com.wa.sdk.demo"

    signingConfigs {
        sdk2config {
            keyAlias 'pcl-sdk-cert-dev'
            keyPassword '123456'
            storeFile file('../keystores/android-cert-dev.jks')
            storePassword '123456'
        }
    }

    defaultConfig {
        applicationId "com.ghw.sdk2"
        minSdkVersion 24
        targetSdkVersion 36
        compileSdk 36
        versionCode 5010000
        versionName "5.1.0"
        signingConfig signingConfigs.sdk2config

        //华为多语种设置
        resConfigs "en", "zh-rCN", "pt-rBR", "es-rES", "in-rID", "ru", "th-rTH", "zh-rHK", "zh-rTW"
        // 默认名称，标题
        resValue "string", "app_name", "WingSDK"
        resValue "string", "title_main", "WingSDK"
    }

    // 声明将使用的构建维度
    flavorDimensions = ["pkg"]

    productFlavors {
        // 包类型
        defaultPackage { dimension = "pkg"; isDefault = true }
        nowgg { dimension = "pkg" }
        leidian { dimension = "pkg" }
        qoo {
            dimension = "pkg"
            applicationIdSuffix = ".qooapp"
        }
    }

    applicationVariants.configureEach { variant ->
        def mergedFlavor = variant.getMergedFlavor()
        def name = variant.name.toLowerCase()

        // 默认构建配置
        def flavor_pkg = 'defaultPackage' // defaultPackage,nowgg,leidian
        def flavor_app = 'sdk2'
        def flavor_env = 'live'
        def isGHG = true
        variant.productFlavors.forEach { flavor ->
            def flavor_dimension = flavor.dimension
            def flavor_name = flavor.name
            if (flavor_dimension == "pkg") {
                flavor_pkg = flavor_name
            }
        }

//        println("Variants name:" + name + " , pkg:" + flavor_pkg + " , app:" + flavor_app + " , env:" + flavor_env)
        // 默认应用 (sdk2)
        def sdk_app_id = "f7f9a9d18da611e5a0be000d3a906774"
        def sdk_app_key = "CFHF7nQCCaojCX6Sm4eT1GEIWRprimgX"
        variant.resValue "string", "sdk_app_id", sdk_app_id
        variant.resValue "string", "sdk_app_key", sdk_app_key

        // 应用名
        def app_name = "WingSDK"
        def title_main = "WingSDK"
        variant.resValue "string", "app_name", app_name
        variant.resValue "string", "title_main", title_main

        // 官网包配置
        variant.resValue "string", "wa_custom_tab_scheme", ("wa" + sdk_app_id)
        def sdk_adid_provider = "com.wa.provider.ad." + name.replace("release", "")
        mergedFlavor.manifestPlaceholders.putAll([SDK_ADID_PROVIDER: sdk_adid_provider])
        mergedFlavor.manifestPlaceholders.putAll([SDK_CHANNEL: isGHG ? "Site" : "Rtwogames"])
//        println('applicationVariants :' + name + ",  " + sdk_adid_provider + " , app_name:" + app_name)


        // 打包文件名
        variant.outputs.each { output ->
            def formattedDate = new Date().format('MMdd_HHmm')
            def output_ver = '_v' + variant.mergedFlavor.versionName
            def output_env = '_' + flavor_env + '环境'
            def output_build_apk = '_build' + formattedDate + '.apk'
            def output_header = 'WingSDK' + output_ver + output_env

            def output_apk_name = output_header
            if (name.contains("nowgg")) {
                output_apk_name += '_Nowgg包'
            } else if (name.contains("leidian")) {
                output_apk_name += '_雷电包'
            } else if (name.contains("qoo")) {
                output_apk_name += '_qoo包'
            }
            output.outputFileName = output_apk_name + output_build_apk
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sdk2config

            def build_time = getDate()
            buildConfigField 'String', 'DEMO_BUILD_TIME', "\"${build_time}\""
        }

        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.sdk2config

            def build_time = getDate()
            buildConfigField 'String', 'DEMO_BUILD_TIME', "\"${build_time}\""
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

//获取时间戳
static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd_HHmm')
    return formattedDate
}

dependencies {
    // Demo 用
    implementation 'com.google.android.material:material:1.12.0'

    // Google包 接入模块
    implementation "com.wa.sdk:wa-sdk-base:5.1.0"         // 基础模块
    implementation "com.wa.sdk:wa-sdk-core:5.1.0"         // 核心模块
    implementation "com.wa.sdk:wa-sdk-aihelp:5.0.0"       // AiHelp客服模块
    implementation "com.wa.sdk:wa-sdk-appsflyer:5.0.0"    // AppsFlyer模块
    implementation "com.wa.sdk:wa-sdk-google:5.1.0"       // Google模块
    implementation "com.wa.sdk:wa-sdk-facebook:5.0.0"     // Facebook模块
    implementation "com.wa.sdk:wa-sdk-firebase:5.0.0"     // Firebase消息推送模块
    implementation "com.wa.sdk:wa-sdk-admob:5.0.0"        // AdMob广告模块（可选）

    // 官网包 接入模块
    implementation "com.wa.sdk:wa-sdk-base:5.1.0"         // 基础模块
    implementation "com.wa.sdk:wa-sdk-core:5.1.0"         // 核心模块
    implementation "com.wa.sdk:wa-sdk-aihelp:5.0.0"       // AiHelp客服模块
    implementation "com.wa.sdk:wa-sdk-appsflyer:5.0.0"    // AppsFlyer模块
    implementation "com.wa.sdk:wa-sdk-ghgl:5.1.0"         // 网页登录模块（官网包必接）
    implementation "com.wa.sdk:wa-sdk-ghgp:5.1.0"         // 网页支付模块（官网包必接）
    implementation "com.wa.sdk:wa-sdk-admob:5.0.0"        // AdMob广告模块（可选）

    // Nowgg包 接入模块（接入时 nowggImplementation 换成 implementation）
    nowggImplementation "com.wa.sdk:wa-sdk-base:5.1.0"            // 基础模块
    nowggImplementation "com.wa.sdk:wa-sdk-core:5.1.0"            // 核心模块
    nowggImplementation "com.wa.sdk:wa-sdk-aihelp:5.0.0"          // AiHelp客服模块
    nowggImplementation "com.wa.sdk:wa-sdk-appsflyer:5.0.0"       // AppsFlyer模块
    nowggImplementation "com.wa.sdk:wa-sdk-nowgg:4.8.0"            // Nowgg模块（Nowgg包必接）

    // 雷电包 接入模块（接入时 leidianImplementation 换成 implementation）
    leidianImplementation "com.wa.sdk:wa-sdk-base:5.1.0"          // 基础模块
    leidianImplementation "com.wa.sdk:wa-sdk-core:5.1.0"          // 核心模块
    leidianImplementation "com.wa.sdk:wa-sdk-appsflyer:5.0.0"     // AppsFlyer模块
    leidianImplementation "com.wa.sdk:wa-sdk-google:5.1.0"        // Google模块
    leidianImplementation "com.wa.sdk:wa-sdk-facebook:5.0.0"      // Facebook模块
    leidianImplementation "com.wa.sdk:wa-sdk-leidian:4.8.0"        // 雷电模块（雷电包必接）

    // Qoo包 接入模块（接入时 qooImplementation 换成 implementation）
    qooImplementation "com.wa.sdk:wa-sdk-base:5.1.0"          // 基础模块
    qooImplementation "com.wa.sdk:wa-sdk-core:5.1.0"          // 核心模块
    qooImplementation "com.wa.sdk:wa-sdk-aihelp:5.0.0"          // AiHelp客服模块
    qooImplementation "com.wa.sdk:wa-sdk-appsflyer:5.0.0"     // AppsFlyer模块
    qooImplementation "com.wa.sdk:wa-sdk-qoo:5.1.0"           // Qoo模块（Qoo包必接）

//    华为 HMS（可选）
//    implementation 'com.wa.sdk:wa-sdk-huaweihms:3.18.0'
    // Aptoide模块（可选）
//    implementation 'com.wa.sdk:wa-sdk-aptoide:3.18.0'
//    wa hup模块（可选，已废弃）
//    implementation 'com.wa.sdk:wa-sdk-hup:3.8.0'
    // 应用墙模块（可选，已废弃）
//    implementation 'com.wa.sdk:wa-sdk-apw:3.8.3'
    // WA广告模块（可选，已废弃）
//    implementation 'com.wa.sdk:wa-sdk-ad:3.8.3'
    // Chartboost模块（可选，已废弃）
//    implementation 'com.wa.sdk:wa-sdk-chartboost:3.9.7'
    // Twitter推送模块（可选，已废弃）
//    implementation 'com.wa.sdk:wa-sdk-twitter:3.9.0'
    // Instagram推送模块（可选，已废弃）
//    implementation 'com.wa.sdk:wa-sdk-instagram:3.8.4'
    // VK模块（可选，已废弃）
//    implementation 'com.wa.sdk:wa-sdk-vk:3.9.0'
}

//谷歌推送
apply plugin: 'com.google.gms.google-services'

//华为
//apply plugin: 'com.huawei.agconnect'
//agcp{
//    enableAPMS true
//}